plugins {
    id "idea"
    id "org.jetbrains.kotlin.kapt" version "1.9.25"
    id "org.jetbrains.kotlin.jvm" version "1.9.25"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.9.25"
    id "org.jetbrains.kotlin.plugin.spring" version "1.9.25"
    id "org.springframework.boot" version "3.3.4"
    id "com.gorylenko.gradle-git-properties" version "2.4.2"
    id "io.spring.dependency-management" version "1.1.6"
}

ext {
    jetbrainsAnnotationsVersion = "24.0.1"
    postgresContainerVersion = "1.20.1"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

[compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "17"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-logging"

    implementation "org.postgresql:postgresql"
    implementation "org.liquibase:liquibase-core"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation "org.jetbrains:annotations:$jetbrainsAnnotationsVersion"

    kapt "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.testcontainers:postgresql:$postgresContainerVersion"
}

gitProperties {
    dateFormat = "dd.MM.yyyy HH:mm:ss"
    dateFormatTimeZone = "Europe/Moscow"
    extProperty = "gitProps"
}

generateGitProperties.outputs.upToDateWhen { false }

processResources {
    filesMatching(["**/properties-from-gradle.properties"]) {
        expand(project.properties)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

bootJar {
    archiveFileName = "${project.name}.jar"
}

jar {
    enabled = false
}
